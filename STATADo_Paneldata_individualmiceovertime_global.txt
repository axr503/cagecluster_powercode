* Panel data models (also called multilevel models) applicable to Cage-Clustered Data
* Written as a file.do for Stata; version 15.1
* Code accompannying article: 
* Understanding artificial mouse-microbiome heterogeneity and six actionable themes to increase study power. Abigail R Basson, Alexandria LaSalla, Gretchen Lam, Danielle Kulpins,  Erika L Moen,  Mark Sundrud, Jun Miyoshi, Sanja Ilic,  Betty R Theriault, Fabio Cominelli, Alexander Rodriguez-Palacios. 2019. doi: https://doi.org/10.1101/778043 

* This code is a brief overview of analytical possibilities to consider cage-clusters of data,
* which may also include repeated measures (blood tests over time on same individual.. 
* Other open access software have similar coding structures to conduct the run the same models.
* Asterisks represent lines intended as comments
* Lines can be turn on/off as live functions by removing/adding the asterisk at the beginning of the line
* This annotation /* $t */ is to turn off the function $t within a longer function line. 
* Removing the /* and */ will turn the function on. 
* Consult a statistician or epidemiologist for advice relevant to your study design/data.
*file name: STATADo_Paneldata_individualmiceovertime_global


/*
Panel data models provide information on individual behavior, both across individuals and over time.  
The data and models have both cross-sectional and time-series dimensions.  
Panel data can be balanced when all individuals are observed in all time periods or unbalanced when individuals are not observed in all time periods. 
Examples include estimating the effect of education on income, with data across time and individuals; 
and estimating the effects of income on savings, with data across years and countries.
*/

clear all
set more off

* finding files, setting working directories. https://www.stata.com/manuals/dcd.pdf

pwd

import delimited "/Users/alexrodriguez/Dropbox/STATA_powertable_cagedataset.txt"


ds
* rowid mouseid timeweeks treatment treatcode response responselog2 clustscen1 clustscen2 clustscen3 clustscen4 clustscen5 clustscen1code clustscen2code clustscen3code clustscen4code clustscen5code meancage_sc1 meancagelog_sc1 meancage_sc2 meancagelog_sc2 meancage_sc3 meancagelog_sc3 meancage_sc4 meancagelog_sc4 meancage_sc5 meancagelog_sc5 aweight_1 aweight_2 aweight_3 aweight_4

global id mouseid
global t timeweeks
global ylist random /*responselog2*/
global xlist treatcode 

describe $id  $ylist $xlist $t
summarize $id $ylist $xlist $t

xtline responselog2, i(mouseid) t(timeweeks)
xtline responselog2, overlay i(mouseid) t(timeweeks)
xtline response, overlay i(mouseid) t(timeweeks)
xtline random, overlay i(mouseid) t(timeweeks)

xtline response, overlay i(clustscen2code) t(timeweeks)

* Set the mouse data as panel data
sort $id  $t
xtset $id $t
xtdescribe
xtsum $id $ylist $xlist $t

* model that runs pooled Ordinary least squares estimator
reg $ylist $xlist

* Population-averaged estimator
xtreg $ylist $xlist, pa

* Between estimator
xtreg $ylist $xlist, be

* Fixed effects or within estimator
xtreg $ylist $xlist, fe

* First-differences estimator
reg D.($ylist $xlist), noconstant

* Random effects estimator
xtreg $ylist $xlist, re theta

* Hausman test for fixed versus random effects model
quietly xtreg $ylist $xlist, fe
estimates store fixed
quietly xtreg $ylist $xlist, re
estimates store random
hausman fixed random

* Breusch-Pagan LM test for random effects versus OLS
quietly xtreg $ylist $xlist, re
xttest0

* Recovering individual-specific effects
quietly xtreg $ylist $xlist, fe
predict alphafehat, u
sum alphafehat
